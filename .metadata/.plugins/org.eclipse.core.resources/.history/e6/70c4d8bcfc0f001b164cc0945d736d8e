scchart DDD_pacemaker {
// signal read from the board
input signal
Vsense,
Asense,
VRP_TO,
PVARP_TO,
LRI_TO,
AEI_TO,
AVI_TO;
// signals emitted to the board
output signal
Vpace,
Apace;

    initial state TOP{  
     
      input signal
      LRI_ex,
      PVARP_start,
      AEI_start,
      AVI_start;
      output signal
      VRP_start,
      VRP_ex,
      LRI_start,
      LRI_stop,
      PVARP_ex,
      AEI_ex,
      AVI_ex;
     
     
     region VRP:
     
     
        initial state init
        --> waitVRP with Vsense/ VRP_start
        --> waitVRP with Vpace/ VRP_start;
 
     
      state waitVRP
      --> init with VRP_ex;
     
     
    region VRP_timer:
     
        initial state idle
      -->timer with VRP_start;
     
      state timer
      -->idle with VRP_TO/ VRP_ex;
   
   
    region LRI:

initial state init
--> waitVRP with Vsense/LRI_start
--> waitVRP with Vpace/LRI_start;

state waitVRP
--> waitLRI with VRP_ex;

state waitLRI
--> reinit with LRI_ex/Vpace
--> reinit with Vsense/LRI_stop;
 
state reinit
--> waitVRP with /LRI_start;

region LRI_timer:

initial state idle
-->timer with LRI_start;

state timer
--> idle with LRI_stop
--> idle with LRI_TO/LRI_ex;

region PVARP:

initial state init
--> waitPVARP with Vpace/PVARP_start
--> waitPVARP with Vsense/PVARP_start;

state waitPVARP
--> waitVense_or_Vpace with PVARP_ex;

state waitVense_or_Vpace
--> init with Asense
--> init with Apace;

region PVARP_timer:

initial state idle
--> timer with PVARP_start;

state timer
--> idle with PVARP_TO/PVARP_ex;

region AEI:

initial state init
-->waitPVARP with Vpace/AEI_start
-->waitPVARP with Vsense/AEI_start;

state waitPVARP
-->waitAsense_or_Apace with PVARP_ex;

state waitAsense_or_Apace
--> init with Apace
--> init with Asense
--> init with AEI_ex/Apace;

region AEI_timer:

initial state idle
--> timer with AEI_start;

state timer
--> idle with AEI_TO/AEI_ex;

region AVI:

initial state init
--> waitAVI_or_Vpace_or_Vsense with Asense/AVI_start
--> waitAVI_or_Vpace_or_Vsense with Apace/AVI_start
--> waitAVI_or_Vpace_or_Vsense with AEI_ex/AVI_start;

state waitAVI_or_Vpace_or_Vsense
--> init with AVI_ex/Vpace
--> init with Vsense;

region AVI_timer:

initial state idle
-->timer with AVI_start;

state timer
--> idle with AVI_TO/AVI_ex
--> idle with Vsense;
    };
}
